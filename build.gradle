apply plugin: 'java'
defaultTasks 'clean', 'test'

ext {
  selenideVersion = '7.0.6'
  junitVersion = '5.10.2'
  assertjVersion = '3.25.2'
  slf4jVersion = '2.0.11'
  littleProxyVersion = '2.0.18'
}

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories{
  mavenCentral()
  mavenLocal()
  maven {
    url uri('https://oss.sonatype.org/content/repositories/snapshots/')
    mavenContent {
      snapshotsOnly()
    }
  }
}

dependencies {
  testImplementation "com.codeborne:selenide:$selenideVersion"
  testImplementation "com.codeborne:selenide-clear-with-shortcut:$selenideVersion"
  testImplementation "com.codeborne:selenide-full-screenshot:$selenideVersion"
  testImplementation "com.codeborne:selenide-appium:$selenideVersion"
  testImplementation "org.jsoup:jsoup:1.17.2"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  testImplementation "org.assertj:assertj-core:$assertjVersion"
  testImplementation "org.slf4j:slf4j-api:$slf4jVersion"
  testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  exclude group: "org.asynchttpclient"
  exclude group: "io.netty"
  exclude group: "io.opentelemetry"
}

task libs(type: Sync) {
  from configurations.testRuntimeClasspath
  into "$buildDir/lib"
}

compileJava.dependsOn libs

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.debug = true
  sourceCompatibility = 21
}

sourceCompatibility = '21'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}


test {
  useJUnitPlatform()
  systemProperties 'file.encoding': 'UTF-8'
  systemProperties 'selenide.browser': 'chrome'
  systemProperties 'selenide.headless': true
  systemProperties 'selenide.pageLoadStrategy': 'none'
  systemProperties['BUILD_URL'] = System.getenv()['BUILD_URL']
  testLogging.showStandardStreams = true
  outputs.upToDateWhen {false}
}
