apply plugin: 'java'
defaultTasks 'clean', 'test'
sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories{
  mavenCentral()
  mavenLocal()
  maven {
    url uri('https://oss.sonatype.org/content/repositories/snapshots/')
    mavenContent {
      snapshotsOnly()
    }
  }
}

dependencies {
  testImplementation 'com.codeborne:selenide:5.25.0-selenium-4.0.0-rc-2'
  testImplementation('org.apache.httpcomponents.client5:httpclient5:5.1') {
    exclude group: 'org.apache.httpcomponents.core5', module: 'httpcore5-h2'
  }
  testRuntimeOnly('com.browserup:browserup-proxy-core:2.1.2') {exclude group: 'io.netty'}
  testRuntimeOnly("io.netty:netty-all:4.1.68.Final") {because 'used by browserup-proxy'}
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
  testImplementation 'org.assertj:assertj-core:3.21.0'
  testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task libs(type: Sync) {
  from configurations.testRuntimeClasspath
  into "$buildDir/lib"
}

compileJava.dependsOn libs

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

test {
  useJUnitPlatform()
  systemProperties 'file.encoding': 'UTF-8'
  systemProperties 'selenide.browser': 'chrome'
  systemProperties 'selenide.headless': true
  systemProperties 'selenide.pageLoadStrategy': 'none'
  systemProperties['BUILD_URL'] = System.getenv()['BUILD_URL']
  testLogging.showStandardStreams = true
}
