apply plugin: 'java'
defaultTasks 'clean', 'test'

ext {
  selenideVersion = '7.13.0'
  junitVersion = '6.0.1'
  assertjVersion = '3.27.6'
  slf4jVersion = '2.0.17'
}

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories{
  mavenCentral()
  mavenLocal()
  maven {
    name = 'Central Portal Snapshots'
    url = 'https://central.sonatype.com/repository/maven-snapshots/'
    mavenContent {
      snapshotsOnly()
    }
    content {
      includeGroup("com.codeborne")
    }
  }
}

dependencies {
  testImplementation "com.codeborne:selenide:$selenideVersion"
  testImplementation "com.codeborne:selenide-clear-with-shortcut:$selenideVersion"
  testImplementation "com.codeborne:selenide-full-screenshot:$selenideVersion"
  testImplementation "com.codeborne:selenide-appium:$selenideVersion"
  testImplementation "org.jsoup:jsoup:1.21.2"

  testImplementation platform("org.junit:junit-bom:$junitVersion")
  testImplementation "org.junit.jupiter:junit-jupiter"
  testImplementation "org.junit.jupiter:junit-jupiter-params"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"

  testImplementation "org.assertj:assertj-core:$assertjVersion"
  testImplementation "org.slf4j:slf4j-api:$slf4jVersion"
  testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

configurations.configureEach {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  exclude group: "org.asynchttpclient"
  exclude group: "io.netty"
  exclude group: "io.opentelemetry"
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.debug = true
  sourceCompatibility = 25
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}


test {
  useJUnitPlatform()
  systemProperties 'file.encoding': 'UTF-8'
  systemProperties 'selenide.browser': 'chrome'
  systemProperties 'selenide.headless': true
  systemProperties 'selenide.pageLoadStrategy': 'none'
  systemProperties['BUILD_URL'] = System.getenv()['BUILD_URL']
  testLogging.showStandardStreams = true
  outputs.upToDateWhen {false}
}
